#
#    Package for managing omniORB "cxy" backend idl compilation.
#    (cxy is an alternate, modern, C++ mapping for CORBA IDL)
#

*.idl.sm => :idl.sm;

:idl.sm '.idl files and nested .idl.sm' ? => :FILE;

:idl.sm.list 'list from :idl.sm' ? => :LIST;

READ-LIST (:idl.sm) => (:idl.sm.list);

:idl.list 'list of idl files' ? => :LIST;

:idl.list.m ':idl.list from :idl.sm' => :idl.list;

COLLECT (:idl.sm.list :recurse=:idl.sm.list) => (:idl.list.m);

:idl.list.s ':idl.list from .idl' => :idl.list;

COLLECT (:IDL) => (:idl.list.s);

:omnicxy.hcp.list 'list of :hcp files generated from .idl file' ? => :hcp.list;

:omnicxy 'directory of :hcp files generated from a .idl file' ? => :DERIVED-DIRECTORY;

+hpath 'path to locate all generated headers, in generated #include statements (see hcp-split -hpath option), eg x/y generates #include <x/y/idl.hh>' ? => :first;

+cxy-be-dir 'directories containing omniidl cxy backend .py files, eg cxy.py, default assumes files are on omniidl default search path (see also $ODIN_OMNICXY_BE_DIR environment variable in omnicxy.dg)' ? => :ls;

$ODIN_OMNICXY_PATH 'PATH to set when running omniidl with cxy backend' = '/usr/bin:/bin';

$ODIN_OMNICXY_BE_DIR 'directory containing omniidl cxy backend .py files, eg cxy.py, default assumes files are on omniidl default search path (see also +cxy-be-dir)' = '';

$ODIN_OMNICXY_OMNIIDL 'name of omniidl program' = 'omniidl';

+cxy 'flags to pass to omniidl cxy backend (omit the -Wb)' ? => :cat;

EXEC (omnicxy.sh) (:IDL) (+hpath) (+cxy-be-dir) (+cxy) (+inc_sp)
  NEEDS (+cxy-be-dir:list:map=:list) (:IDL:lookup=:hash_inc_all)
        ($ODIN_OMNICXY_BE_DIR/cxy.py)
        ($ODIN_OMNICXY_BE_DIR/cxycref.py)
        ($ODIN_OMNICXY_BE_DIR/cxyobjref.py)
        ($ODIN_OMNICXY_BE_DIR/cxycdr.py)
        ($ODIN_OMNICXY_BE_DIR/cxysref.py)
        ($ODIN_OMNICXY_BE_DIR/cxyany.py)
  => (:omnicxy);

READ-LIST (:omnicxy) => (:omnicxy.hcp.list);


:omnicxy.virdir-spec 'odinfile with virtual targets for hh and cc files generated from a .idl file'? => :FILE;

:omnicxy.virdir-specs 'list of :omnicxy.virdir-spec'? => :REFERENCE;

:omnicxy.virdir 'virtual directory hh and cc generated from .idl.sm'? => :REFERENCE;

COLLECT (:omnicxy.virdir-specs:cat:vir_dir) => (:omnicxy.virdir);

COLLECT (:idl.list:map=:omnicxy.virdir-spec) => (:omnicxy.virdir-specs);

#hh-ext, cc-ext, basename, hpath come from hcp.dg
EXEC (omnicxy.virdir-spec.sh) (:IDL)@ (:omnicxy)@ (+hh-ext) (+cc-ext) (+basename) (+hpath) => (:omnicxy.virdir-spec);

:omnicxy.targets-spec 'odinfile with targets for hh and cc files generated from a .idl file'? => :FILE;

:omnicxy.targets-specs 'list of :omnicxy.target-spec'? => :REFERENCE;

:omnicxy.targets 'odinfile covering hh and cc files generated from idl.sm'? => :REFERENCE;

COLLECT (:idl.list:map=:omnicxy.targets-spec) => (:omnicxy.targets-specs);

#hh-ext, cc-ext, basename, hpath come from hcp.dg
EXEC (omnicxy.targets-spec.sh) (:IDL)@ (:omnicxy)@ (+hh-ext) (+cc-ext) (+basename) (+hpath) => (:omnicxy.targets-spec);

COLLECT (:omnicxy.targets-specs:cat) => (:omnicxy.targets);
