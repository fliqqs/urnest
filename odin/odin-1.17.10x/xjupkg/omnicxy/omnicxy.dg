#
#    Package for managing omniORB "cxy" backend idl compilation.
#    (cxy is an alternate, modern, C++ mapping for CORBA IDL)
#

*.idl.sm => :idl.sm;

:idl.sm '.idl files and nested .idl.sm' ? => :FILE;

:idl.sm.list 'list from :idl.sm' ? => :LIST;

:idl.list 'list of idl files' ? => :LIST;

:idl.list.m ':idl.list from :idl.sm' => :idl.list;

:idl.list.s ':idl.list from .idl' => :idl.list;

:omnicxy 'vir-dir of files generated from .idl files' ? => :REFERENCE;

:omnicxy.output 'directory of files generated from a .idl file' ? => :DERIVED-DIRECTORY;

:hcpsplit.output 'directory of files generated from a .idl file' ? => :DERIVED-DIRECTORY;

:omnicxy.vir_dir_spec 'vir dir spec with all files in :hcpsplit.output directory' ? => :FILE;

:omnicxy.vir_dir_spec.s ':omnicxy.vir_dir_spec from single .idl file' ? => :omnicxy.vir_dir_spec;

#:omnicxy.vir_dir_spec.m ':omnicxy.vir_dir_spec from .idl.sm' ? => :omnicxy.vir_dir_spec;

+hcp-split 'hcp-split exe, default to hcp-split on PATH' ? => :first;

+hpath 'path to locate all generated headers, in generated #include statements (see hcp-split -hpath option), eg x/y generates #include <x/y/idl.hh>' ? => :first;

+cxy-be-dir 'directories containing omniidl cxy backend .py files, eg cxy.py, default assumes files are on omniidl default search path (see also $ODIN_OMNICXY_BE_DIR environment variable in omnicxy.dg)' ? => :ls;

$ODIN_OMNICXY_PATH 'PATH to set when running omniidl with cxy backend' = '/usr/bin:/bin';

$ODIN_OMNICXY_BE_DIR 'directory containing omniidl cxy backend .py files, eg cxy.py, default assumes files are on omniidl default search path (see also +cxy-be-dir)' = '';

$ODIN_OMNICXY_OMNIIDL 'name of omniidl program' = 'omniidl';

+cxy 'flags to pass to omniidl cxy backend (omit the -Wb)' ? => :cat;

+ntc 'pass to -ntc to hcp-split, disabling file-and-line tracking in generated cc files' ? => :first;

EXEC (omniidl.sh) (:IDL) (+hpath) (+cxy-be-dir) (+cxy) (+inc_sp)
  NEEDS (+cxy-be-dir:list:map=:list) (:IDL:lookup=:hash_inc_all)
        ($ODIN_OMNICXY_BE_DIR/cxy.py)
        ($ODIN_OMNICXY_BE_DIR/cxycref.py)
        ($ODIN_OMNICXY_BE_DIR/cxyobjref.py)
        ($ODIN_OMNICXY_BE_DIR/cxycdr.py)
        ($ODIN_OMNICXY_BE_DIR/cxysref.py)
  => (:omnicxy.output);

EXEC (hcpsplit.sh) (:omnicxy.output) (+hcp-split) (+hpath) (+ntc)
  NEEDS (:omnicxy.output:list)
  => (:hcpsplit.output);

EXEC (vir_dir_spec.sh) (:hcpsplit.output) => (:omnicxy.vir_dir_spec.s);

COLLECT (:IDL) => (:idl.list.s);

READ-LIST (:idl.sm) => (:idl.sm.list);

COLLECT (:idl.sm.list :recurse=:idl.sm.list) => (:idl.list.m);

COLLECT (:idl.list :map=:omnicxy.vir_dir_spec.s :cat:vir_dir) => (:omnicxy);
