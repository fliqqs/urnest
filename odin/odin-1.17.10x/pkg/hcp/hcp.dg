#
# to add targets to hcp-split all .hcp files in current directory, leaving them
# as .hh and .cc files in the current directory, add to ./Odinfile
#
#   == (.:hcp.list:hcp-split)
#
# to split just one .hcp file, producing a derived directory with .hh and
# .cc files in it: xxx.hcp :hcp-split
#
$ODIN_HCP_PATH 'path for hcp-split' = '/usr/bin:/bin';
$ODIN_HCP_LD_LIBRARY_PATH 'LD_LIBRARY_PATH for hcp-split' = '';

*.hcp => :hcp;

*.hcp.sm  => :hcp.sm;

*.smap => :smap;

:hcp.sm 'hcp files tree spec'? => :FILE;

:hcp.sm.list 'list from hcp.sm' => :LIST;

:hcp.list 'list of :hcp files' => :LIST;

:hcp.sm.hcp.list 'list of :hcp files from :hcp.sm' => :hcp.list;

:dir.hcp.list 'list of :hcp files in a directory'? => <:hcp.list>;

:hcp.hh 'C++ header file from hcp-split'? => :REFERENCE;
:hcp.hh.ref 'file naming :hcp.hh'? => :FILE;

:hcp.cc 'C++ implementation file from hcp-split'? => :REFERENCE;
:hcp.cc.ref 'file naming :hcp.cc'? => :FILE;

:hcp-split 'directory of files generated by hcp-split'? => :DERIVED-DIRECTORY;

:smap 'map of file line to source file and line'? => :FILE;

:hcp.hh.smap 'file mapping :hcp.hh line numbers to source file line numbers'? => :REFERENCE;

:hcp.cc.smap 'file mapping :hcp.cc line numbers to source file line numbers'? => :REFERENCE;

:hcp.hh.smap.ref 'file containing name name of :hcp.hh.smap'? => :FILE;

:hcp.cc.smap.ref 'file containing name name of :hcp.cc.smap'? => :FILE;

:hcp-split-virdir-spec 'virdir spec covering hcp-split generated files'? => :FILE;

:hcp-split-virdir-specs 'list of :hcp-split-virdir-spec'? => :REFERENCE;

:hcp-split-spec 'odinfile covering hcp-split generated files'? => :FILE;

:hcp-split-specs 'list of :hcp-split-spec'? => :REFERENCE;

:hcp-split.odin 'odin file with targets for :h and :cxx generated from :hcp.list'? => :REFERENCE;


+hh-ext 'extension to give generated hh file, default .hh'? => :first;

+cc-ext 'extension to give generated cc file, default .cc'? => :first;

+hpath 'hpath option to hcp-split'? => :first;

+hcp-split 'hcp-split exe, default to hcp-split on ODIN_HCP_PATH' ? => :first;

+basename 'base name for files generated by hcp-split, overriding computed default, eg +basename=fred generates fred.hh, fred.cc'? => :first;

+G 'include \\"generated-as\\" comments in generated hh/cc'? => :first;

READ-LIST (:hcp.sm)
   => (:hcp.sm.list);

COLLECT (:hcp.sm.list :recurse=:hcp.sm.list)
   => (:hcp.sm.hcp.list);

COLLECT (:FILE:list:extract=:hcp) => (:dir.hcp.list);

COLLECT (:hcp.list:map=:hcp-split-virdir-spec) => (:hcp-split-virdir-specs);

:which-hcp-split 'hcp-split exe from +hcp-split or ODIN_HCP_PATH hcp-split'
  => :REFERENCE;
:which-hcp-split-viewspec 'viewspec to locate hcp-split exe' => :FILE;

EXEC (which-hcp-split-viewspec.sh) (+hcp-split) => (:which-hcp-split-viewspec);

COLLECT (:which-hcp-split-viewspec:view:first) => (:which-hcp-split);

EXEC (hcpsplit.sh)  (:hcp) (:which-hcp-split) (+hh-ext) (+cc-ext) (+basename) (+hpath) (+G) => (:hcp-split);

EXEC (hcp-split-virdir-spec.sh) (:hcp)@  (+hh-ext) (+cc-ext) (+basename) (:hcp-split)@ => (:hcp-split-virdir-spec) (:hcp.hh.ref) (:hcp.cc.ref) (:hcp.hh.smap.ref) (:hcp.cc.smap.ref);

EXEC (hcp-split-spec.sh) (:hcp)@  (+hh-ext) (+cc-ext) (+basename) (:hcp.hh)@ (:hcp.cc)@ => (:hcp-split-spec);

READ-REFERENCE (:hcp.hh.ref) => (:hcp.hh);

READ-REFERENCE (:hcp.cc.ref) => (:hcp.cc);

READ-REFERENCE (:hcp.hh.smap.ref) => (:hcp.hh.smap);

READ-REFERENCE (:hcp.cc.smap.ref) => (:hcp.cc.smap);

COLLECT (:hcp.list:map=:hcp-split-spec) => (:hcp-split-specs);

COLLECT (:hcp-split-specs:cat) => (:hcp-split.odin);
