#
# Abstract
#
#    Package for C++ tags using hcp-tags.
#    
#

$ODIN_HCP_TAGS_PATH 'PATH to set when running hcp tags' = '/usr/bin:/bin';

$ODIN_HCP_TAGS 'name of the hcp-tags program' = 'hcp-tags';

$ODIN_HCP_TAGS_MERGE 'name of the hcp-tags-merge program' = 'hcp-tags-merge';

*.hcp => :hcp;
*.list-of-tags => :list-of-tags;
:tags.sm 'manual list of :tags' => :FILE;

:hcp 'hcp file' => :FILE;
:tags 'hcp-tags file'? => :FILE;
:htags 'hcp-tags from a :h' => :tags;
:hcptags 'hcp-tags from a :hcp' => :tags;
:merged-tags ':tags file merged togeter to for a new :tags file'? => :tags;

:list-of-tags 'file listing :tags file'? => :FILE;
:list-of-tags.list 'list from :list-of-tags ' => :LIST;

:dir_taggable ':h and :hcp files of a directory' => :LIST;
:dir.list-of-tags 'list of :tags for a directory ie all header and hcp files in a directory'? => :list-of-tags.list;

:merged_tags ':tags file from :list-of-tags'? => :tags;

:which-hcp-tags 'hcp-tags exe from +hcp-tags or ODIN_HCP_TAGS_PATH'
  => :REFERENCE;
:which-hcp-tags-viewspec 'viewspec to locate hcp-tags exe' => :FILE;

+hcp-tags 'hcp-tags exe, default to ODIN_HCP_TAGS on ODIN_HCP_TAGS_PATH' ? => :first;

EXEC (which-hcp-tags-viewspec.sh) (+hcp-tags) => (:which-hcp-tags-viewspec);

COLLECT (:which-hcp-tags-viewspec:view:first) => (:which-hcp-tags);

:which-hcp-tags-merge 'hcp-tags-merge exe from +hcp-tags-merge or ODIN_HCP_TAGS_PATH'
  => :REFERENCE;
:which-hcp-tags-merge-viewspec 'viewspec to locate hcp-tags-merge exe' => :FILE;

+hcp-tags-merge 'hcp-tags-merge exe, default to ODIN_HCP_TAGS_MERGE on ODIN_HCP_TAGS_PATH' ? => :first;

EXEC (which-hcp-tags-merge-viewspec.sh) (+hcp-tags-merge) => (:which-hcp-tags-merge-viewspec);

COLLECT (:which-hcp-tags-merge-viewspec:view:first) => (:which-hcp-tags-merge);


#:FILE here is a directory
COLLECT (:FILE:list:extract=:h) (:FILE:list:extract=:hcp) => (:dir_taggable);
COLLECT (:dir_taggable:map=:tags) => (:dir.list-of-tags);

EXEC (hcp-tags.sh) (:h) 'htags' (:which-hcp-tags) => (:htags);
EXEC (hcp-tags.sh) (:hcp) 'hcptags' (:which-hcp-tags) => (:hcptags);

READ-LIST (:list-of-tags) => (:list-of-tags.list);

EXEC (hcp-tags-merge.sh) (:which-hcp-tags-merge) (:list-of-tags.list:ls) 'merged-tags'
  NEEDS (:list-of-tags.list)
  => (:merged-tags);
